apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 14
        //noinspection OldTargetApi
        targetSdkVersion 30
        versionCode 67
        versionName createVersionName(versionCode)

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            android.libraryVariants.all { variant ->
                variant.outputs.all { outputFileName = "android-dialog.aar" }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                '-module-name',
                "cn.cqray.android.dialog",
                "-Xjvm-default=all",
        ]
    }

    buildFeatures {
        viewBinding = true
    }

    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.6.0'
            force "androidx.core:core-ktx:1.6.0"
        }
    }
}

dependencies {
    api fileTree(dir: "libs", include: ["*.jar"])
    //noinspection GradleDependency
    api 'androidx.appcompat:appcompat:1.3.1'
    // 动画
    api 'com.gitee.cqray:android-animator:0.7.0'
    //noinspection GradleDependency
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // 流式布局
    api 'com.google.android:flexbox:2.0.1'
    // ButterKnife
    compileOnly 'com.jakewharton:butterknife:10.2.3'
}

afterEvaluate {

    generateReleaseBuildConfig.enabled = false
    generateDebugBuildConfig.enabled = false

    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                // You can then customize attributes of the publication as shown below.
                groupId = 'com.github.cqray'
                artifactId = 'android-dialog'
            }
        }
    }
}

/**
 * 生成版本名
 */
static String createVersionName(int code) {
    int num1 = code / 100
    int num2 = (code - num1 * 100) / 10
    int num3 = code - num1 * 100 - num2 * 10
    return num1 + "." + num2 + "." + num3
}
